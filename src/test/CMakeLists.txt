function(raygame_test _target)
    set(options NO_MAIN)
    set(oneValueArgs)
    set(multiValueArgs SOURCES DEPENDS)
    cmake_parse_arguments(RAYGAME_TESTARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
    set(RAYGAME_TEST_USE_MAIN $<NOT:$<BOOL:${RAYGAME_TESTARGS_NO_MAIN}>>)
    add_executable(${_target})
    target_compile_options(${_target} PRIVATE -Wno-unsafe-buffer-usage)
    target_link_libraries(${_target} PUBLIC ${RAYGAME_TESTARGS_DEPENDS})
    target_link_libraries(${_target} PUBLIC doctest::doctest)
    target_sources(
        ${_target} PUBLIC $<${RAYGAME_TEST_USE_MAIN}:${RAYGAME_PATH_SRC}/test/main.cpp> ${RAYGAME_TESTARGS_SOURCES}
    )
    raygame_add_args(${_target})
    raygame_add_defs(Core)
    add_test(NAME ${_target} COMMAND ${_target})
endfunction()

raygame_test(TestTests SOURCES test.cpp DEPENDS RayGame::Core)
raygame_test(TestMath SOURCES math.cpp DEPENDS RayGame::Core::Math)
raygame_test(TestGenericMath SOURCES math.cpp DEPENDS RayGame::Core::Math)
target_compile_definitions(TestGenericMath PUBLIC "RAYGAME_FORCE_GENERIC_IMPL")
